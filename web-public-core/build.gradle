plugins {
    id "com.moowork.node" version "0.12"
    id "com.moowork.gulp" version "0.12"
}

dependencies {
    compile project(":web-admin-core")

    testCompile('org.gradle:gradle-tooling-api:2.13-rc-1')
    testCompile('com.jayway.restassured:rest-assured:2.9.0')
}


// Web resource handling

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('bower_components')
    dependsOn npmInstall
}

node {
    version = '5.9.0'
    download = true
}

gulp {
    workDir = file("${project.projectDir}")
    colors = true
    bufferOutput = false
}

task webResourceBuild {
    dependsOn installGulp, npmInstall, bowerInstall, gulp_build
}

gulp_build.mustRunAfter installGulp
gulp_build.mustRunAfter npmInstall
gulp_build.mustRunAfter bowerInstall
processResources.dependsOn gulp_localBuild
build.dependsOn webResourceBuild


// Functional test running

task updateWebDriver(type: NodeTask) {
    script = file('node_modules/protractor/bin/webdriver-manager')
    args = ['update', '--standalone']
}
task protractorTests(type: NodeTask) {
    script = file('node_modules/protractor/bin/protractor')
    args = ['src/test/resources/protractor.conf.js']
}
