plugins {
    id "com.moowork.node" version "0.12"
    id "com.moowork.gulp" version "0.12"
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.data:spring-data-mongodb')
    compile('com.mangofactory:swagger-springmvc:1.0.2')

    // Web Jars
    compile('ro.isdc.wro4j:wro4j-core:1.8.0')
    compile('ro.isdc.wro4j:wro4j-extensions:1.8.0')
    compile('org.webjars:swagger-ui:2.1.8-M1')
}

// Web resource handling

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('bower_components')
    dependsOn npmInstall
}

node {
    version = '5.9.0'
    download = true
}

gulp {
    workDir = file("${project.projectDir}")
    colors = true
    bufferOutput = false
}

task webResourceBuild {
    dependsOn installGulp, npmInstall, bowerInstall, gulp_build
}

gulp_build.mustRunAfter installGulp
gulp_build.mustRunAfter npmInstall
gulp_build.mustRunAfter bowerInstall
processResources.dependsOn gulp_localBuild
build.dependsOn webResourceBuild